pluginManagement {
	includeBuild "gradle/plugins/aot-smoke-test-plugin"
	repositories {
		gradlePluginPortal()
		maven {
			url "https://repo.spring.io/snapshot"
		}
		maven {
			url 'https://repo.spring.io/milestone'
		}
		maven {
			url "https://repo.spring.io/release"
			content {
				includeGroup "io.spring.ge.conventions"
			}
		}
	}
}

plugins {
	id "com.gradle.enterprise" version "3.12.6"
	id "io.spring.ge.conventions" version "0.0.13"
}

rootProject.name="spring-aot-smoke-tests"

include "aot-smoke-test-support"
include "aot-smoke-test-third-party-hints"

["authorization-server", "batch", "boot", "cloud", "data", "framework", "graphql", "integration", "security", "session"].each { group ->
	file(group).eachDirMatch(~/[a-z].*/) { smokeTest ->
		include "$group:${smokeTest.name}"
	}
}
